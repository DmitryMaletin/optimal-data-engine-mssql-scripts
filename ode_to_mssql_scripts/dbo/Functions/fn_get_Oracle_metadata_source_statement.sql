CREATE FUNCTION [dbo].[fn_get_Oracle_metadata_source_statement]
(
	@sourceDatabaseName sysname,
	@sourceDatabaseSchema sysname,
	@sourceDatabaseTable sysname,
	@metadataUse CHAR(3), -- Valid values (at the moment) are hub or stg.
		-- This parameter will switch the ordinal position of a field
		-- between bk_ordinal_position (hub) and satellite_ordinal_position (stg).
	@keyDetectionType VARCHAR(20) -- Valid values are Primary, Unique, None
		-- This determines whether additional filtering conditions are added to the
		-- SQL logic in order to just query for the Primary or Unique keys.

)
/********************************************************************************************
The purpose of this function is to return a SQL statement that can be executed against a source
system database (Oracle) in order to return metadata about the specified source table 
for use in the ODE_Config database.

The eventual output from this generated query will go into the ODE_Config user-defined table type
[dbo].[dv_column_type].

At the moment the query that is generated by this function is used within an open query and so in
order for this to handle the additional layer of string parsing we need to triple escape any
quotation marks used to identify string values.  
TO DO - replace this with an option via parameter.

Use this function to obtain an SQL statement that can be executed against a source database system
in order to obtain metadata for a tables fields, or a tables primary/unique keys.

Example uses
=============
-- Staging tables
SELECT [dbo].[fn_get_MSSQL_metadata_source_statement] ('CLTNET', 'CLTNET', 'TB_CLT_POOL', 'stg', NULL)

-- Hub table (Primary Keys)
SELECT [dbo].[fn_get_MSSQL_metadata_source_statement] ('CLTNET', 'CLTNET', 'TB_CLT_POOL', 'hub', 'Primary')

-- Hub table (Unique Keys)
SELECT [dbo].[fn_get_MSSQL_metadata_source_statement] ('CLTNET', 'CLTNET', 'TB_CLT_POOL', 'hub', 'Unique')

SELECT [dbo].[fn_get_MSSQL_metadata_source_statement] ('CLTNET', 'CLTNET', 'TB_CLT_POOL', 'hub', 'blah')

// NOTE // - The last example for a hub table will return a SQL statement designed to not return
	any records as there are no keys desired.

********************************************************************************************/
RETURNS varchar(4000)
AS
BEGIN
DECLARE @SQL	VARCHAR(MAX) = ''
       ,@crlf	CHAR(2)	= CHAR(13) + CHAR(10)

IF @metadataUse = 'hub'
BEGIN
	-- The following query allows querying of a remote database for metadata about a tables
	-- primary key, or unique index.

	SET @SQL = '
	SELECT 
		cols.COLUMN_NAME, 
		tc.DATA_TYPE, 
		tc.DATA_LENGTH, 
		tc.DATA_PRECISION, 
		tc.DATA_SCALE,
		'''''''' collation_name,
		cols.POSITION bk_ordinal_position, 
		0 source_ordinal_position,
		0 satellite_ordinal_position,
		''''''''  abbreviation,
		''''hub''''	object_type
	
	FROM all_constraints cons
	JOIN all_cons_columns cols
		ON cons.constraint_name = cols.constraint_name
		AND cons.owner = cols.owner
	JOIN all_tab_columns tc
		ON cons.table_name = tc.table_name
		AND cons.owner = tc.owner
		AND cols.column_name = tc.column_name
	WHERE 1=1
		AND cons.STATUS = ''''ENABLED''''
	' + @crlf

	IF @keyDetectionType = 'Unique'
		SET @SQL = @SQL + 'AND cons.CONSTRAINT_TYPE = ''''U''''' + @crlf

	ELSE IF @keyDetectionType = 'Primary'
		SET @SQL = @SQL + 'AND cons.CONSTRAINT_TYPE = ''''P''''' + @crlf

	ELSE SET @SQL = @SQL + 'AND 1=2' + @crlf

	SET @SQL = @SQL + '
		AND cons.OWNER = ''''' + @sourceDatabaseSchema + ''''' 
		AND cols.TABLE_NAME = ''''' + @sourceDatabaseTable + '''''' 
	
	END

ELSE IF @metadataUse = 'stg'
BEGIN
	SET @SQL = 
	'SELECT 
		COLUMN_NAME,
		DATA_TYPE,
		DATA_LENGTH,
		DATA_PRECISION,
		DATA_SCALE,
		'''''''' collation_name,
		0 bk_ordinal_position,
		COLUMN_ID source_ordinal_position,
		0 satellite_ordinal_position,
		'''''''' abbreviation,
		''''stg'''' object_type

	FROM all_tab_columns 
	WHERE (1=1)
		AND OWNER = ''''' + @sourceDatabaseSchema + '''''
		AND TABLE_NAME =  ''''' + @sourceDatabaseTable + ''''''  

END

ELSE
BEGIN
	SET @SQL = 'raise_application_error( -20001, ''''Invalid Metadata Use (%s) Provided [' + @metadataUse +']'')' 
	-- This statement is hopefully designed to be executed on the source database system.
END

-- Return the resulting SQL Query.
RETURN @SQL

END

GO